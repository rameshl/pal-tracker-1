
import org.flywaydb.gradle.task.FlywayMigrateTask
buildscript {
    dependencies {
        classpath 'mysql:mysql-connector-java:8.0.13'
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id 'java-library'
    id "org.flywaydb.flyway" version "5.2.1"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'mysql:mysql-connector-java'
}

dependencies {
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

springBoot {
    mainClassName = "io.pivotal.pal.tracker.PalTrackerApplication"
}

jar {
    enabled = true
}

bootJar {
    manifest {
        attributes 'Start-Class': 'io.pivotal.pal.tracker.PalTrackerApplication'
    }
}

test {
    useJUnitPlatform()
}

def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=root&password=Priv@te2&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
bootRun.environment([
        "WELCOME_MESSAGE": "hello",
        "SPRING_DATASOURCE_URL": developmentDbUrl,
        "MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE": "*",
        "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS": "always",
])

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=root&password=Priv@te2&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
        "WELCOME_MESSAGE": "Hello from test",
        "SPRING_DATASOURCE_URL": testDbUrl,
        "MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE": "*",
        "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS": "always",
])

flyway {
    url = developmentDbUrl
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}

springBoot {
    buildInfo()
}